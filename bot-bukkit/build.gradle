plugins {
    id "com.gradleup.shadow" version "8.3.6"
}

repositories {
    maven {
        url 'https://libraries.minecraft.net/'
    }
    maven {
        name "mineralDevPrivate"
        url "https://repo.mineral.gg/private"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                classifier = ""
            }
        }
    }
}

dependencies {
    compileOnly 'gg.mineral.mineralspigot:mineralspigot-server:20250122-21.00.59'
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    implementation project(':bot-base-client')
    compileOnly project(':bot-shared-library')
    compileOnly project(':bot-api')
}

shadowJar {
    relocate('net.minecraft', 'gg.mineral.minecraft') {
        exclude 'net.minecraft.server.v1_8_R3.**'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    configurations = [project.configurations.runtimeClasspath]
}

jar {
    enabled = false
}

tasks.build.dependsOn tasks.shadowJar
