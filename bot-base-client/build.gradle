plugins {
    id 'java'
}

group 'gg.mineral'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net/'
    }
}

dependencies {

    compileOnly project(':bot-api')
    implementation project(':bot-shared-library')
    implementation project(':bot-intelligence')

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    compileOnly 'io.netty:netty-all:4.1.112.Final'
    compileOnly 'it.unimi.dsi:fastutil:8.5.14'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.23.1'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.23.1'
    implementation 'net.java.dev.jna:jna:5.14.0'
    implementation 'net.java.dev.jna:jna-platform:5.14.0'
    implementation 'com.ibm.icu:icu4j:75.1'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'com.paulscode:codecjorbis:20101023'
    implementation 'com.paulscode:codecwav:20101023'
    implementation 'com.paulscode:libraryjavasound:20101123'
    implementation 'com.paulscode:librarylwjglopenal:20100824'
    implementation 'com.paulscode:soundsystem:20120107'
    implementation 'com.google.guava:guava:33.2.1-jre'
    implementation 'org.apache.commons:commons-lang3:3.16.0'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.lwjgl.lwjgl:lwjgl:2.9.3'
    implementation 'org.lwjgl.lwjgl:lwjgl_util:2.9.3'
    implementation 'com.mojang:authlib:1.5.21'
    implementation 'javax.vecmath:vecmath:1.5.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.compilerArgs += ['-parameters']
}

sourceSets {
    main.java.srcDirs += 'src/main/mc-client'
    main.resources.srcDirs += 'src/main/mc-resources'
}

jar {
    manifest {
        attributes 'Multi-Release': 'true'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

import java.io.File
import java.util.UUID

apply plugin: 'application'

// Set the main class for the application
mainClassName = 'gg.mineral.bot.base.client.StandaloneLauncher'

// Define a custom task to run the Minecraft client
task runMinecraftClient {
    doLast {
        // Define the temporary directory for Minecraft
        def tempDir = System.getProperty("java.io.tmpdir")
        def minecraftDir = new File(tempDir, "minecraft_temp")

        // Ensure the directory exists
        if (!minecraftDir.exists()) {
            if (minecraftDir.mkdirs()) {
                println "Created temporary Minecraft directory at ${minecraftDir.absolutePath}"
            } else {
                throw new RuntimeException("Failed to create temporary Minecraft directory")
            }
        }

        // Define the location of the natives directory relative to the build.gradle file
        def nativesDir = new File(projectDir, "natives")

        // Run the Minecraft client with the constructed arguments
        javaexec {
            main = mainClassName
            classpath = sourceSets.main.runtimeClasspath
            jvmArgs = ['-Djava.library.path=' + nativesDir.absolutePath]
        }
    }
}

// Make the task depend on the build task
runMinecraftClient.dependsOn build
