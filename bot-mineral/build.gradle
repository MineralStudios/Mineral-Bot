plugins {
    id 'org.jetbrains.kotlin.jvm'
    id "com.gradleup.shadow" version "9.3.0"
}

kotlin {
    jvmToolchain(21)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                classifier = ""
            }
        }
    }
}

repositories {
    maven {
        url 'https://libraries.minecraft.net/'
    }
    maven {
        name "mineralDevPrivate"
        url "https://repo.mineral.gg/private"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    compileOnly 'io.netty:netty-all:4.2.9.Final'
    compileOnly "gg.mineral.engine:api:20251222-19.26.09"
    implementation project(':bot-base-client')
    compileOnly project(':bot-shared-library')
    compileOnly project(':bot-api')
}

// Disable the default jar task.
jar {
    enabled = false
}

// Configure shadowJar as your final jar.
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Multi-Release': 'true'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.resources.srcDirs) {
        into('/')
    }
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

// Optionally, make sure that assembling your project depends on the shadowJar task.
assemble.dependsOn shadowJar
