plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' apply false
}

// Apply Kotlin plugin to Kotlin projects
configure(subprojects.findAll { 
    it.name in ['bot-api', 'bot-base-client', 'bot-bukkit', 'bot-intelligence', 
                'bot-lwjgl-bridge', 'bot-mineral', 'bot-shared-library', 'bot-standalone'] 
}) {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    
    kotlin {
        jvmToolchain(21)
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    compileJava {
        sourceCompatibility = '21'
        targetCompatibility = '21'
        options.compilerArgs += ['-parameters']
    }

    group = "gg.mineral"
    version = new Date().format('yyyyMMdd')

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = project.name
                version = new Date().format("yyyyMMdd-HH.mm.ss")
            }
        }

        repositories {
            maven {
                name = "mineralDevPrivate"
                url = uri("https://repo.mineral.gg/private")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}
