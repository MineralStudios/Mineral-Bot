plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '2.1.21' apply false
    id 'org.jetbrains.kotlin.plugin.lombok' version '2.1.21' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    // Default to Java 17, but allow bot-mineral to override
    if (project.name != 'bot-mineral') {
        compileJava {
            sourceCompatibility = '17'
            targetCompatibility = '17'
            options.compilerArgs += ['-parameters']
        }
    } else {
        compileJava {
            sourceCompatibility = '21'
            targetCompatibility = '21'
            options.compilerArgs += ['-parameters']
        }
    }

    group = "gg.mineral"
    version = new Date().format('yyyyMMdd')

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = project.name
                version = new Date().format("yyyyMMdd-HH.mm.ss")
            }
        }

        repositories {
            maven {
                name = "mineralDevPrivate"
                url = uri("https://repo.mineral.gg/private")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}
